# Core definitions of builtin classes and opcodes.

class None # 0: class of fake nullptr object.
  # no contents
class SmallInt # 1: Inside OOP with the low bit set.
  # no contents
class Bytes # 2: arbitrary bytes (of varying length).
  byte[] flex
class Str # 3: string and/or byte buffer
  byte[] flex  # first byte is len.
class Bytecodes # 4: Python bytecodes (of varying length).
  byte[] flex
class MC # 5: 6809/6309 Machine Code (of varying length).
  byte[] flex
class Array # Array (of varying length) of OOPs.
  oop[] flex
# class Chain # a chain for an use.
#   oop root
#   small len2
class List # a chain for a list.
  small len2
  oop next
  oop[] flex
class Tuple # just like List.
  small len2
  oop next
  oop[] flex
class Dict # a chain for a dict.
  small len2
  oop next
  oop[] flex
class Module # a Module is made from a Dict.
  small len2
  oop next
  oop[] flex
class Frame
  oop prev_frame
  oop function
  small nargs
  small prev_sp
  small prev_ip
  oop tries
  oop[] flex
class Class
  small classNum
  oop className
  oop methDict
  oop fieldList
class Try
  small catch_loc
  oop next
class ListIter
  oop train
  small i
class DictIter
  oop train
  small i
class StrIter
  oop base
  oop p
  small i
  small len
class File
  small fd
class FileIter
  small fd

class Pair
  oop first
  oop second

class Train
  small len2
  oop next
  oop[] flex
class TrainIter
  oop train
  small i

# Opcodes.
code  Abort  # Abort comes first, so 00 means abort.
  [
    assert0(0, "ABORT OP");
  ]
code  Stop  # Stop comes first, so 00 means stop.
  in
  out
  [
    return;
  ]
code  Nop
  in
  out
  [
  ]
code  LitInt
  arg lit
  in
  out z
  [
    z = ( Q(lit) );
  ]
code  LitInt2
  arg hi lo
  in
  out z
  [
    word lit = ((word)hi<<8) + lo;
    z = ( Q(lit) );
  ]
code  LitStr
  arg nth
  in
  out z
  [
    z = ( TrainGetNth(InternList, nth) );
  ]
code  LitStr2
  arg hi lo
  in
  out z
  [
#if 0
    word nth = ((word)hi << 8) | (word)lo;
    z = ( LongTrainGetNth(InternList, nth) );
#else
    RaiseC("not_imp");
#endif
  ]
code  Plus
  in a b
  out z
  [
    if ((a&1) && (b&1)) {
      int aa = N(a);
      int bb = N(b);
      z = ( Q(aa + bb) );
    } else if ((a&1) || (b&1)) {
      RaiseC("bad_plus_mixed");
    } else if (ocls(a)==C_Str && ocls(b)==C_Str) {
      z = StrCat2(a, b);
    } else {
      RaiseC("bad_plus");
    }
  ]
code  Minus
  in a b
  out z
  [
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa - bb) );
  ]
code  Times
  in a b
  out z
  [
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa * bb) );
  ]
code  Div
  in a b
  out z
  [
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa / bb) );
  ]
code  Mod
  in a b
  out z
  [
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa % bb) );
  ]
code  SimplePrint
  in a
  out
  [
#if unix
    // is hard to see with so much debugging output.
    // make it more obvious
    printf("\n@@@@@ PRINT @@@@@ ");
#endif
    SimplePrint(a);
    fflush(stdout);
  ]
code  Assert
  in a
  out
  [
    assert(Truth(a));
  ]
code  GlobalDict
  in
  out z
  [
    z = GlobalDict;
  ]
code  GlobalGet
  arg nth
  in
  out z
  [
    word addr = TrainAddrOfNth(GlobalDict, nth);
    assert(addr);
    z = ogetw(addr);
  ]
code  GlobalPut
  arg nth
  in a
  out
  [
    word addr = TrainAddrOfNth(GlobalDict, nth);
    assert(addr);
    oputw(addr, a);
  ]
code  Branch
  arg whither
  in
  out
  [
        ip = function + whither;
  ]
code  BranchIfFalse
  arg whither
  in pred
  out
  [
        if (!Truth(pred)) ip = function + whither;
  ]
code  BranchIfTrue
  arg whither
  in pred
  out
  [
        if (Truth(pred)) ip = function + whither;
  ]
code Not
  in a
  out z
[
  byte x = (byte) !Truth(a);
  z = FROM_INT(x);
]
code Is
  in a b
  out z
[
  byte x = (a == b);
  z = FROM_INT(x);
]
code ShiftLeft
  in a b
  out z
[
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa << bb) );
]
code ShiftRight
  in a b
  out z
[
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa >> bb) );
]
code BitAnd
  in a b
  out z
[
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa & bb) );
]
code BitOr
  in a b
  out z
[
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa | bb) );
]
code BitXor
  in a b
  out z
[
    int aa = N(a);
    int bb = N(b);
    z = ( Q(aa ^ bb) );
]
code  EQ
  in a b
  out z
  [
    bool p = Equal(a, b);
    z = Q(p);
  ]
code  NE
  in a b
  out z
  [
    bool p = !Equal(a, b);
    z = Q(p);
  ]
code  LT
  in a b
  out z
  [
    cmp_t p = (-1 == Compare(a, b));
    z = Q(p);
  ]
code  GT
  in a b
  out z
  [
    cmp_t p = (+1 == Compare(a, b));
    z = Q(p);
  ]
code  LE
  in a b
  out z
  [
    cmp_t p = (+1 != Compare(a, b));
    z = Q(p);
  ]
code  GE
  in a b
  out z
  [
    cmp_t p = (-1 != Compare(a, b));
    z = Q(p);
  ]

code ArgGet
  arg i
  out z
  [
    z = ArgGet(i);
  ]
code ArgPut
  arg i
  in a
  [
    ArgPut(i, a);
  ]

code LocalGet
  arg i
  out z
  [
    z = (  Frame_flex_At(fp, i) );
  ]

code LocalPut
  arg i
  in a
  [
    Frame_flex_AtPut(fp, i, a);
  ]

code Drop
  in unused
  [
    {}
  ]

code Call
  arg nargs
  [
   Call(nargs, PopSp());  // don't use `in`
  ]

code CallMeth
  arg meth_isn nargs
  [
    CallMeth(meth_isn, nargs);
  ]

code Return
  [
    Return(PopSp());  // don't use `in`
  ]
code  RetSelf
  [
    Return(ArgGet(0));
  ]
code  RetNone
  [
    Return(None);
  ]
code Construct
  arg cls_num nargs
  [
    Construct(cls_num, nargs);
  ]
code Dup
  in x
  out x1 x2
  [
    x1 = x2 = x;
  ]
code MemberGet
  arg member
  in obj
  out result
  [
    word z = MemberGet(obj, member);
    result = z;
  ]
code SelfMemberGet
  arg offset
  out result
  [
    word obj = ArgGet(0);
    assert(offset < ocap(obj));
    word addr = obj + offset;
    result = ogetw(addr);
  ]
code MemberPut
  arg member
  in value obj
  [
    MemberPut(obj, member, value);
  ]
code SelfMemberPut
  arg offset2
  in value
  [
    byte offset = offset2<<1;
    word obj = ArgGet(0);
    assert(offset < ocap(obj));
    oputw(obj + offset, value);
  ]
code NewTuple
  arg llen
  [
    Implode(llen, NewTuple());
    // Implode(llen, NewTuple(llen));
  ]
code NewList
  arg llen
  [
    Implode(llen, NewList());
  ]
code NewDict
  arg llen
  [
    Implode(llen<<1, NewDict());
  ]
code Len
  in obj
  out length
  [
    int n = Len(obj);
    length = FROM_INT(n);
  ]
code GetItem
  in coll key
  out value
[
  value = GetItem(coll, key);
]
code PutItem
  in value coll key
[
  PutItem(coll, key, value);
]
code Ord
  in x
  out z
  [
    word s = x;
    CHECK(ocls(s) == C_Str, "ord");
    CHECK(StrLen(x), "ord");
    byte ch = StrAt(x, 0);
    z = FROM_INT(ch);
  ]
code Chr
  in x
  out z
  [
    word n = TO_INT(x);
    CHECK(n>=0, "chr");
    CHECK(n<=255, "chr");
    z = SingletonStr((byte)n);
  ]
code Try
  arg catch_loc
[
  DoTry(catch_loc);
]
code EndTry
  arg end_catch_loc
[
  // Pop the `tries` entry and branch to after the Catch block.
  // The Catch instruction always follows.
  DoEndTry(end_catch_loc);
]
code Catch
[
  // A no-op just to make bytecode traces more readable.
  // This is the landing point afer a Raise in a Try block.
  // Once we arrive here, the exception object is on the stack,
  // and the exception is considered handled, from the point
  // of view of the bytecode engine.
]
code Raise
[
   Raise(PopSp());  // Don't use `in`
]
code Explode
  arg len
[
  Explode(len);
]
code Range
  in n_
  out list
[
  int n = TO_INT(n_);
  word z = NewList();
  for (int i=0; i<n; i++) {
    ListAppend(z, FROM_INT(i));
    SayObj(z, 3);
  }
  list = z;
]
code Import
  in name
[
  RaiseC("todo_import");
]
code Directory
[
  Directory();  // A demo/debugging opcode.
]
code SpecialStdin
  out z
[
  z = Stdin;
]
code SpecialStdout
  out z
[
  z = Stdout;
]
code SpecialStderr
  out z
[
  z = Stderr;
]
code Open
  in mode filename
  out z
[
  z = PyOpenFile(filename, mode);
]
code SpecialTrue
  out z
[
  z = FROM_INT(true);
]
code SpecialFalse
  out z
[
  z = FROM_INT(false);
]
code SpecialNone
  out z
[
  z = None;
]
code Negate
  in a
  out z
[
  int x = - TO_INT(a);
  z = FROM_INT(x);
]
code Int
  in a
  out z
[
  z = BuiltinInt(a);
]
code Str
[
  word a = PopSp();
  word z = BuiltinStr(a);
  if (z) PushSp(z);  // if None, don't touch stack.
]
code Repr
  in a
  out z
[
  z = BuiltinStr(a);
]
code Sorted
  in a
  out z
[
  z = BuiltinSorted(a);
]
code GC
  out z
[
  // DumpStats();
  ogc();
  // DumpStats();
  z = None;
]
code ForkShellAndWait
  out z
[
  byte x = ForkShellAndWait();
  z = FROM_INT(x);
]
code GetAndClearSignal
  out z
[
  z = FROM_INT(signalled);
  signalled = 0;
]
code For
[
  DoFor();
]
code Next
[
  DoNext();
]
code HandleStopIteration
  arg end_while
[
  DoHandleStopIteration(end_while);
]
code ShedTryBlocks
  arg n
[
  ShedTryBlocks(n);
]

code UserSetJmp
  out val
[
  word z = (word) osetjmp(user_jmp_buf);
  val = z;
]
code UserLongJmp
  in val
[
  olongjmp(user_jmp_buf, val);
]

# # # # # #

prim Str __str__
  in self
  out z
[
  z = self;
]

prim Str rstrip
  in self
  out z
[
  z = StrRStrip(self);
]

prim Str upper
  in self
  out z
[
  z = StrUpper(self);
]

prim List __iter__
  in self
  out z
[
  z = NewListIter(self);
]

prim ListIter next
  in self
  out z
[
  z = ListIterNext(self);
]

prim Tuple __iter__
  in self
  out z
[
  // ListIter works for Tuple.
  z = NewListIter(self);
]

prim Dict __iter__
  in self
  out z
[
  z = NewDictIter(self);
]

prim DictIter next
  in self
  out z
[
  z = DictIterNext(self);
]

prim Module __iter__
  in self
  out z
[
  // Dict iter works for Module.
  z = NewDictIter(self);
]

prim Module __getattr__
  in self key
  out z
[
  // TODO: Should we use __getattr__ or hardwire Module?
  z = DictGet(self, key);
]

prim File __iter__
  in self
  out z
[
  z = oalloc(FileIter_Size, C_FileIter);
  FileIter_fd_Put(z, File_fd(self));
]

prim FileIter next
  in self
  out z
[
  z = FileReadLineToNewBuf(self);
  if (!z) RaiseC("StopIteration");
]
prim File readline
  in self
  out line
[
  line = FileReadLineToNewBuf(self);
]
prim File writeline
  in line self
  out z
[
  FileWriteLine(self, line);
  z = None;
]
prim File close
  in self
  out z
[
  RaiseC("unimp");
  z = None;
]
prim File flush
  in self
  out z
[
  z = None;
]
prim Dict items
  in self
  out z
[
  z = DictItems(self);
]
